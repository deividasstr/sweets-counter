apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'

apply from: rootDir.absolutePath + '/base_lib.gradle'
apply from: rootDir.absolutePath + '/utils.gradle'

apply plugin: 'kotlin-allopen'
apply plugin: 'kotlin-android'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
    }
}
allOpen {
    annotation 'com.deividasstr.data.utils.OpenClass'
}

enableJacoco(project, 'Debug')

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
android {
    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            releaseConfig {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    defaultConfig {
        buildConfigField 'String', 'BASE_API_URL', getString(BASE_API_URL)
        applicationId "com.deividasstr.sweetscounter"
        testInstrumentationRunner "com.deividasstr.ui.utils.TestAppRunner"
    }
    dataBinding {
        enabled = true
    }
    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        String sharedTestDir = 'src/sharedTest/kotlin'
        String sharedTestResDir = 'src/sharedTest/res'
        test.java.srcDirs += sharedTestDir

        androidTest.java.srcDirs += sharedTestDir
        androidTest.res.srcDirs += sharedTestResDir
    }
    buildTypes {
        release {
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.releaseConfig
            }
        }
    }

    androidExtensions {
        experimental = true
    }

}

dependencies {
    implementation project(':data')
    implementation project(':devices')
    implementation project(':MPChartLib')

    implementation 'androidx.core:core-ktx:1.0.1'
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "android.arch.work:work-runtime-ktx:$work_version"
    implementation "android.arch.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "android.arch.navigation:navigation-ui-ktx:$navigationVersion"
    implementation "androidx.paging:paging-runtime-ktx:$pagingVersion"

    // Spring animation
    implementation "androidx.dynamicanimation:dynamicanimation:$supportLibraryVersion"

    //Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    implementation "com.jakewharton.timber:timber:$timberVersion"

    //UI
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.tbuonomo:morph-bottom-navigation:$morphNavBarVersion"
    implementation "com.robinhood.ticker:ticker:$tickerVersion"
    implementation "com.jaychang:simplerecyclerview:$simpleRecyclerViewVersion"
    implementation "com.jaychang:simplerecyclerview-kotlin-android-extensions:$simpleRecyclerViewVersion"

    //implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
    implementation 'com.google.firebase:firebase-perf:16.2.3'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    //Tests
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation "androidx.arch.core:core-testing:$lifecycleVersion"
    testImplementation "org.amshove.kluent:kluent:$kluentVersion"
    testImplementation "io.objectbox:objectbox-linux:$objectboxVersion"
    testImplementation "io.objectbox:objectbox-macos:$objectboxVersion"
    testImplementation "io.objectbox:objectbox-windows:$objectboxVersion"
    testImplementation "androidx.paging:paging-common-ktx:$pagingVersion"
    testImplementation "com.jraska.livedata:testing-ktx:$livedataTestVersion"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "android.arch.work:work-testing:$work_version"
    androidTestImplementation "androidx.test:rules:$runnerVersion"
    androidTestImplementation "androidx.test:runner:$runnerVersion"
    androidTestImplementation "org.mockito:mockito-android:$mockitoAndroidVersion"
    androidTestImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:' + mockitoKotlinVersion
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
    androidTestImplementation "android.arch.navigation:navigation-testing-ktx:$navigationTestVersion"
    androidTestImplementation project(path: ':domain', configuration: 'testOutput')
    androidTestImplementation "androidx.arch.core:core-testing:$lifecycleVersion"
    androidTestImplementation "com.google.dagger:dagger:$daggerVersion"
    androidTestImplementation "com.github.fabioCollini.daggermock:daggermock-kotlin:$daggerMockVersion"
}

apply plugin: 'com.google.gms.google-services'

