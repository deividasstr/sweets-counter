// Top-level build file where you can add configuration options common to all sub-projects/modules.
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
buildscript {

    ext.kotlin_version = '1.3.21'
    ext.objectboxVersion = '2.2.0'
    ext.gradleVersion = '3.4.0-rc01'
    ext.navigationVersion = '1.0.0-alpha07'

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.objectbox:objectbox-gradle-plugin:$objectboxVersion"
        classpath "android.arch.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'com.google.firebase:firebase-plugins:1.1.5'
        classpath 'io.fabric.tools:gradle:1.27.1'
        classpath 'com.google.gms:google-services:4.2.0'
        classpath 'org.jacoco:org.jacoco.core:0.8.2'
    }
}

plugins {
    id "com.palantir.jacoco-full-report" version "0.4.0"
}

apply from: 'dependencies.gradle'

//http://engineering.rallyhealth.com/android/code-coverage/testing/2018/06/04/android-code-coverage.html
apply from: rootProject.file('jacoco.gradle')

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "http://objectbox.net/beta-repo/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url 'https://jitpack.io' }
        maven { url "https://dl.bintray.com/lion4ik/maven" }
    }

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.github.shyiko:ktlint:0.28.0"
    }

    // https://github.com/google/dagger/issues/306
    afterEvaluate {
        tasks.withType(JavaCompile.class) {
            options.compilerArgs << "-Xmaxerrs" << "500"
        }
    }

    tasks.withType(Test) {
        testLogging {
            events TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_ERROR,
                    TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}